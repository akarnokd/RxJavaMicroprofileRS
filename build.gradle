buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath "biz.aQute.bnd:biz.aQute.bnd.gradle:6.4.0"
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.25.1'
        classpath "gradle.plugin.com.hierynomus.gradle.plugins:license-gradle-plugin:0.15.0"
    }
}

apply plugin: 'java'

apply plugin: 'jacoco'
jacoco {
    toolVersion = "0.8.5"
}

build.dependsOn jacocoTestReport

check.dependsOn jacocoTestReport

javadoc {
    failOnError = false
    
    options.links(
        "http://docs.oracle.com/javase/8/docs/api/",
        "http://reactivex.io/RxJava/3.x/javadoc"
    )
}

group = "com.github.akarnokd"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    implementation "org.eclipse.microprofile.reactive-streams-operators:microprofile-reactive-streams-operators-core:3.0"
    implementation "io.reactivex.rxjava3:rxjava:3.1.6"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.testng:testng:7.5'
    testImplementation "org.eclipse.microprofile.reactive-streams-operators:microprofile-reactive-streams-operators-tck:3.0"
}


apply plugin: 'maven'

apply plugin: 'biz.aQute.bnd.builder'

jar {
    bnd ('Bundle-Name': 'rxjava3-microprofile-rs',
            'Bundle-Vendor': 'akarnokd',
            'Bundle-Description': 'BRxJava 3 Microprofile Reactive Streams Operators',
            'Import-Package': '!org.junit,!junit.framework,!org.mockito.*,!org.testng,*',
            'Bundle-DocURL': 'https://github.com/akarnokd/RxJavaMicroprofileRS')
}

apply plugin: "com.vanniktech.maven.publish"

test {
    maxHeapSize = "1g"
    testLogging  {
        events "started", "failed", "standard_error" // "skipped", "passed"
        exceptionFormat="full"
        //    showStandardStreams = true
    }
    jacoco.includes = ["**akarnokd**"]
}


task testng(type: Test) { 
     useTestNG()
     testLogging  {
        events=["skipped", "failed"]
        exceptionFormat="full"

        debug.events = ["skipped", "failed"]
        debug.exceptionFormat="full"

        info.events = ["failed", "skipped"]
        info.exceptionFormat="full"
        
        warn.events = ["failed", "skipped"]
        warn.exceptionFormat="full"
     }
} 

check.dependsOn testng 

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
    executionData testng
}

jacocoTestReport.dependsOn test
jacocoTestReport.dependsOn testng

apply plugin: "com.github.hierynomus.license"

license {
    header rootProject.file('HEADER')
    skipExistingHeaders true
    ignoreFailures true
    excludes(["**/*.md", "**/*.txt"])
}
